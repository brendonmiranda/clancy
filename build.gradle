buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("io.spring.javaformat:spring-javaformat-gradle-plugin:0.0.38")
	}
}

plugins {
	id 'org.springframework.boot' version '2.6.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "com.palantir.docker" version "0.33.0"
}

apply plugin: 'io.spring.javaformat'

group = 'io.github.brendonmiranda'
version = '2.0.1'
sourceCompatibility = '11'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
	maven { // JDA
		name 'm2-dv8tion'
		url 'https://m2.dv8tion.net/releases'
	}

	maven { // JDA-Chewtils
		url "https://m2.chew.pro/releases"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'net.dv8tion:JDA:5.2.1'
	implementation 'pw.chew:jda-chewtils:1.24.1'
	implementation 'com.sedmelluq:lavaplayer:1.3.78'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.amqp:spring-rabbit-test'
}

docker {
	name "${project.group}/${bootJar.baseName}:$project.version"
	tag "DockerHub", "brendonmiranda/${bootJar.baseName}:$project.version"
	copySpec.from("build/libs").into("/")
	copySpec.from("start.sh").into("/")
	buildArgs([BUILD_VERSION: "$project.version",JAR_BASE_NAME: "${bootJar.baseName}"])
}

test {
	useJUnitPlatform()
	// it should pass all JVM options to the app started via test gradle task
	systemProperties = System.properties // https://stackoverflow.com/questions/25079244/how-to-pass-jvm-options-from-bootrun/25079415
}